// lingo is a technology to make it easy to construct Domain Dpecific Languages or parsers.
// It is a language for specifying grammas that can be used for parsing

exp = num | sub | mult | add | div | var | pow | neg;

// we add bindings using ":" and actions in {} to construct the result

add     = "(" ws exp:l ws "+" ws exp:r ws ")"       { Add(:l, :r) };
sub     = "(" ws exp:l ws "-" ws exp:r ws ")"       { Subtract(:l, :r) };
mult    = "(" ws exp:l ws "*" ws exp:r ws ")"       { Multiply(:l, :r) };
div     = "(" ws exp:l ws "/" ws exp:r ws ")"       { Divide(:l, :r) };
pow     = "(" ws exp:l ws "^" ws exp:r ws ")"       { Power(:l, :r) };
neg     = "(" ws "-" ws exp:e ws ")"                { Negative(:e) };

var     = (letter (letter | digit)*) $v             { Variable($v) };
num     = ("-"? ((digit* "." digit+) | digit+)) $d  { Number(s2d($d)) };

// character range
letter = 'a'-'z' | 'A'-'Z' | '_';
// digit range 
digit = '0'-'9';
// zero, one or more spaces are allowed              
ws = ' '*;